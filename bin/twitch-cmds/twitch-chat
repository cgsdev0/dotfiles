#!/bin/zsh

CHAN=$1
source ~/.twitch_secrets_bot;

rm -f /tmp/twitch_tunnel;
mkfifo /tmp/twitch_tunnel;

# Example chat msg:
# @badge-info=;badges=;client-nonce=31ab40a7bef7162bfc8eade860eb185a;color=#DAA520;display-name=BuddysPizza;emotes=;first-msg=0;flags=;id=e86038a5-4d49-4868-aa96-b563da8fc689;mod=0;room-id=56931496;subscriber=0;tmi-sent-ts=1643181677955;turbo=0;user-id=42089909;user-type= :buddyspizza!buddyspizza@buddyspizza.tmi.twitch.tv PRIVMSG #badcop_ :!help

ld_state_file="$HOME/ludum-dare/$CHAN/.ld-jam-list"
ld_data_file="$HOME/ludum-dare/$CHAN/.ld-jam-data"
ld_raffle_file="$HOME/ludum-dare/$CHAN/.ld-jam-raffle"

mkdir -p $(dirname "$ld_state_file")
mkdir -p $(dirname "$ld_data_file")
mkdir -p $(dirname "$ld_raffle_file")

touch "$ld_state_file"
touch "$ld_data_file"
touch "$ld_raffle_file"


# Following regex is based on https://www.rfc-editor.org/rfc/rfc3986#appendix-B with
# additional sub-expressions to split authority into userinfo, host and port
#
readonly URI_REGEX='^(([^:/?#]+):)?(//((([^:/?#]+)@)?([^:/?#]+)(:([0-9]+))?))?(/([^?#]*))(\?([^#]*))?(#(.*))?'
#                    ↑↑            ↑  ↑↑↑            ↑         ↑ ↑            ↑ ↑        ↑  ↑        ↑ ↑
#                    |2 scheme     |  ||6 userinfo   7 host    | 9 port       | 11 rpath |  13 query | 15 fragment
#                    1 scheme:     |  |5 userinfo@             8 :…           10 path    12 ?…       14 #…
#                                  |  4 authority
#                                  3 //…

parse_scheme () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[2]}"
}

parse_authority () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[4]}"
}

parse_user () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[6]}"
}

parse_host () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[7]}"
}

parse_port () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[9]}"
}

parse_path () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[10]}"
}

parse_rpath () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[11]}"

}

parse_query () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[13]}"
}

parse_fragment () {
    [[ "$@" =~ $URI_REGEX ]] && echo "${match[15]}"
}

colorize() {
    printf "\x1b[38;2;$(( 16#${1:0:2} ));$(( 16#${1:2:2} ));$(( 16#${1:4:2} ))m"
}

classify_fish() {
    n=$1
    if ((n == 1)); then
        echo "LEGENDARY"
    elif ((n >= 1 && n <= 5)); then
        echo "EPIC"
    elif ((n >= 6 && n <= 15)); then
        echo "Rare"
    elif ((n >= 16 && n <= 25)); then
        echo "Uncommon"
    else
        echo "Common"
    fi
}

reply() {
    colorize "ff0000" 1>&2
    echo -n "bot" 1>&2
    printf '\033[0m' 1>&2
    echo -n ": " 1>&2
    echo "$@" 1>&2
    echo "PRIVMSG #${CHAN} :$@\r\n"
}

parsecmd() {
    msg=$(echo -n "$1" | grep -oP "PRIVMSG.*$" | cut -d':' -f2- | tr '[:upper:]' '[:lower:]')
    name=$(echo -n "$1" | grep -oP "display-name=.*?;" | cut -d'=' -f2- | tr -d ';\n')
    case $msg in
        *"goodcop_"*)
            reply "@$name don't at me"
            ;;
        *"do it live"*)
            reply "heck yea @$name thecop2Do thecop2It thecop2Live1 thecop2Live2"
            ;;
        "!endraffle")
            if ! test -f "${ld_raffle_file}_LOCK"; then
                reply "no raffle started. did you mean to do !raffle?"
                return
            fi
            rm -f "${ld_raffle_file}_LOCK"
            winner=$(shuf "$ld_raffle_file" | head -n1)
            # mark any in progress games as played
            sed -i "s/IN_PROGRESS$/PLAYED/" $ld_state_file

            next_game=$(cat "$ld_state_file" | grep -vE " PLAYED$" | grep -E "\\w* $winner" | head -n1)
            next_game_slug=$(echo "$next_game" | cut -d' ' -f1)
            next_game_twitch=$(echo "$next_game" | cut -d' ' -f2)
            if [[ "$next_game" == "" ]]; then
                reply "something has gone horribly wrong NotLikeThis"
                return
            fi
            resp=$(grep -E "^$next_game_slug" "$ld_data_file" | cut -d' ' -f2-)
            entry_type=$(echo "$resp" | jq -r '.type')
            name=$(echo "$resp" | jq -r '.name')
            author=$(echo "$resp" | jq -r '.author')
            friends=$(echo "$resp" | jq -r '.authorCount')
            slug=$(echo "$resp" | jq -r '.slug')
            url=$(shorten-url \
                "https://ldjam.com/events/ludum-dare/50/$next_game_slug" \
                $(echo "$author" | tr '[:upper:]' '[:lower:]') \
                | jq -r '.shortCode')
            if [[ "$friends" != "1" ]]; then
                fc="$((friends-1))"
                author="$author and $fc friends"
            fi
            # update state
            sed -i "s/^\\($slug \\w*\\)$/\\1 IN_PROGRESS/" $ld_state_file
            reply "@$winner has won the raffle! Please introduce yourself and your game if you want :)"
            reply "Up next! '$name' by $author ($entry_type entry) https://cgs.dev/0/$url"
            ;;
        "!raffle")
            if test -f "${ld_raffle_file}_LOCK"; then
                reply "a raffle is already going, end it with !endraffle first pls"
                return
            fi
            if [[ "$name" != "$CHAN" ]] && [[ "$name" != "badcop_" ]]; then
                return
            fi
            touch "${ld_raffle_file}_LOCK"
            rm -f "$ld_raffle_file"
            touch "$ld_raffle_file"
            reply "A raffle has begun! type !join to enter (you must already have your game in the queue to join)"
            ;;
        "!join")
            if ! test -f "${ld_raffle_file}_LOCK"; then
                return
            fi

            if (cat "$ld_raffle_file" | cut -d' ' -f2 | grep -q "$name"); then
                return
            fi
            queue_names=$(cat "$ld_state_file" | grep -vE " PLAYED$" | grep -vE " IN_PROGRESS$" | cut -d' ' -f2)
            if ! ( echo "$queue_names" | grep -q "$name" ); then
                reply "@$name you need to paste the link to your game to register first!"
                return
            fi
            echo "$name" >> "$ld_raffle_file"
            ;;
        # support ludum dare submission links
        *"https://ldjam.com/events/ludum-dare/50/"*)
            position_data=$(cat "$ld_state_file" | grep -vE " PLAYED$")
            uri=$(echo "$msg" | grep -oE "https://ldjam.com/[^ ]+")
            id=$(parse_path "$uri")
            # reply  "https://api.ldjam.com/vx/node2/walk/1${id}?node&author"
            resp=$(curl -Ss "https://api.ldjam.com/vx/node2/walk/1${id}?node&author" | jq -rc '{ type: .node[0].subsubtype, name: .node[0].name, author: .node[1].name, authorCount: .node[0].meta.author | length, slug: .node[0].slug }')
            entry_type=$(echo "$resp" | jq -r '.type')
            gamename=$(echo "$resp" | jq -r '.name')
            author=$(echo "$resp" | jq -r '.author')
            friends=$(echo "$resp" | jq -r '.authorCount')
            slug=$(echo "$resp" | jq -r '.slug')
            position=$( {cat "$ld_state_file"; echo "---"} | grep -vcE " PLAYED$" | cut -d':' -f1)
            if grep -q "$slug" "$ld_state_file"; then
                pos=$(echo "$position_data" | grep -n "$slug" | cut -d':' -f1)
                if [[ "$pos" == "" ]]; then
                    reply "We've already played that game! :O"
                else
                    reply "I already have that game :)"
                fi
                return
            fi
            # append to state file
            echo "$slug $name" >> "$ld_state_file"
            echo "$slug $resp" >> "$ld_data_file"

            if [[ "$friends" != "1" ]]; then
                fc="$((friends-1))"
                author="$author and $fc friends"
            fi

            # de-dupe data file
            cat "${ld_data_file}" | sort | uniq > "${ld_data_file}2"
            mv "${ld_data_file}2" "${ld_data_file}"

            reply "Thanks! I've added the $entry_type game '$gamename' to the queue (position: #$position)! (Made by $author) @$CHAN can use !next to advance the queue"
            ;;
        "!game")
            next_game=$(cat "$ld_state_file" | grep " IN_PROGRESS$")
            if [[ "$next_game" == "" ]]; then
                reply "no more games in the queue NotLikeThis"
                return
            fi
            next_game_slug=$(echo "$next_game" | cut -d' ' -f1)
            next_game_twitch=$(echo "$next_game" | cut -d' ' -f2)
            resp=$(grep -E "^$next_game_slug" "$ld_data_file" | cut -d' ' -f2-)
            entry_type=$(echo "$resp" | jq -r '.type')
            gamename=$(echo "$resp" | jq -r '.name')
            author=$(echo "$resp" | jq -r '.author')
            friends=$(echo "$resp" | jq -r '.authorCount')
            slug=$(echo "$resp" | jq -r '.slug')
            url=$(shorten-url \
                "https://ldjam.com/events/ludum-dare/50/$next_game_slug" \
                $(echo "$author" | tr '[:upper:]' '[:lower:]') \
                | jq -r '.shortCode')
            if [[ "$friends" != "1" ]]; then
                fc="$((friends-1))"
                author="$author and $fc friends"
            fi
            reply "We are playing '$gamename' by $author ($entry_type entry) :D https://cgs.dev/0/$url"
            ;;
        "!queue")
            pos=$(grep -vE " PLAYED$" "$ld_state_file" | grep -vE " IN_PROGRESS$" | cut -d' ' -f2 | grep -n "$name" | head -n1 | cut -d':' -f1)
            pos2=$(grep -E " IN_PROGRESS$" "$ld_state_file" | cut -d' ' -f2 | grep -n "$name")
            unplayed=$(grep -vE " PLAYED$" "$ld_state_file" | wc -l)
            if [[ "$unplayed" == "0" ]]; then
                reply "There are no games in the queue. Yours can be next! Just paste the link in chat :)"
                return
            fi
            queuestr="Uploading current queue..."
            if [[ "$pos2" != "" ]]; then
                reply "Your game is being played right now! :O | $queuestr"
            elif [[ "$pos" == "" ]]; then
                reply "You are not in the queue. Paste the link in chat and I'll add it! | $queuestr"
            else
                reply "Your game is #$pos in the queue :) | $queuestr"
            fi
            reply "$(grep -vE "PLAYED$" "$ld_state_file" | cut -d' ' -f1 | sed 's@^@https://ldjam.com/events/ludum-dare/50/@' | nc termbin.com 9999)"
            ;;
        "!next")
            if [[ "$name" != "$CHAN" ]] && [[ "$name" != "badcop_" ]]; then
                return
            fi

            # mark any in progress games as played
            sed -i "s/IN_PROGRESS$/PLAYED/" $ld_state_file

            next_game=$(cat "$ld_state_file" | grep -vE " PLAYED$" | head -n1)
            next_game_slug=$(echo "$next_game" | cut -d' ' -f1)
            next_game_twitch=$(echo "$next_game" | cut -d' ' -f2)
            if [[ "$next_game" == "" ]]; then
                reply "no more games in the queue NotLikeThis"
                return
            fi
            resp=$(grep -E "^$next_game_slug" "$ld_data_file" | cut -d' ' -f2-)
            entry_type=$(echo "$resp" | jq -r '.type')
            name=$(echo "$resp" | jq -r '.name')
            author=$(echo "$resp" | jq -r '.author')
            friends=$(echo "$resp" | jq -r '.authorCount')
            slug=$(echo "$resp" | jq -r '.slug')
            url=$(shorten-url \
                "https://ldjam.com/events/ludum-dare/50/$next_game_slug" \
                $(echo "$author" | tr '[:upper:]' '[:lower:]') \
                | jq -r '.shortCode')
            if [[ "$friends" != "1" ]]; then
                fc="$((friends-1))"
                author="$author and $fc friends"
            fi
            # update state
            sed -i "s/^\\($slug \\w*\\)$/\\1 IN_PROGRESS/" $ld_state_file
            reply "Up next! '$name' by $author ($entry_type entry) https://cgs.dev/0/$url"
            reply "if @$next_game_twitch is still here, say hi!"
            ;;
        "!ping")
            reply "pong!"
            ;;
        "!hi")
            reply "/me waves"
            ;;
        "!fish leaderboard")
            cd "$HOME/fishing-data/$CHAN"
            reply "$(wc -l * | head -n -1 | sort -nr | head -n 5 | paste -sd '|' | sed 's/|/ | /g')"
            ;;
        "!discord")
            reply "join our discord! https://discord.gg/m2ZzF8CQrW"
            ;;
        "!fish")
            now=$(date +%s)
            if [[ -f "$HOME/fishing-cooldowns/.$name.cooldown" ]]; then
                cooldown=$(cat "$HOME/fishing-cooldowns/.$name.cooldown")
                if [[ $cooldown -gt $now ]]; then
                    return
                fi
            fi
            fish=$(shuf -n 1 $HOME/fish)
            most_count=$(cat ~/fishing-data/${CHAN}/* \
                | sort \
                | uniq -c \
                | sort -nr \
                | head -n 1 \
                | grep -oE '[0-9].*' \
                | cut -d' ' -f1)
            count=$(cat $HOME/fishing-data/${CHAN}/* | grep "$fish" | wc -l)
            expected_count=$(cat $HOME/fish \
                | sort \
                | uniq -c \
                | sort -n \
                | awk '{$1=$1};1' \
                | grep "$fish" \
                | cut -d' ' -f1)
            classification=$(classify_fish $expected_count)
            description="a"
            touch "$HOME/fishing-data/${CHAN}/$name"
            personalcount=$(cat "$HOME/fishing-data/${CHAN}/$name" | grep "$fish" | wc -l)
            if [[ $personalcount -eq 0 ]]; then
                description="their first"
            fi

            rarity=$(bc <<< "scale=2; 100 - ( $count / $most_count * 100 )")
            echo "$fish" >> "$HOME/fishing-data/${CHAN}/$name"
            echo $(( now + 120 )) > "$HOME/fishing-cooldowns/.$name.cooldown"
            if [[ $count -eq 0 ]]; then
                reply "@$name caught $description $fish ($classification)! ( never caught before :O )"
            else
                reply "@$name caught $description $fish ($classification)! ($rarity% rarity)"
            fi
            ;;
        "!help")
            ;&
        "!commands")
            reply "!fish, !fishdex, !roll, !count, !play, !discord, !today, !jump"
            ;;
        "!today")
            reply "$(cat $HOME/.today)"
            ;;
        "!jump")
            reply "/me jumps"
            ;;
        "!fishdex all")
            total=$(cat $HOME/fish | sort -u | wc -l)
            count=$(cat $HOME/fishing-data/${CHAN}/* | sort -u | wc -l)
            reply "$count out of $total possible fish have been caught."
            ;;
        "!fishdex")
            total=$(cat $HOME/fish | sort -u | wc -l)
            touch "$HOME/fishing-data/${CHAN}/$name"
            count=$(cat "$HOME/fishing-data/${CHAN}/$name" | sort -u | wc -l)
            reply "@$name has caught $count out of $total possible fish!"
            ;;
        "!count")
            count=$(cat ~/.twitch_counter)
            count=$(( count + 1 ))
            echo "$count" > ~/.twitch_counter
            reply "$count"
            ;;
        "!roll"*)
            # !roll 20
            val=$(echo -n "$msg" | cut -d' ' -f2)
            re='^[0-9]+$'
            if ! [[ $val =~ $re ]] ; then
                reply "not a valid number!"
            else
                reply "$((1 + $RANDOM % $val))"
            fi
            ;;
        "!submit")
            reply "just drop an ldjam.com link in chat and i'll add it to the queue :)"
            ;;
        "!song")
            reply "@$name i have no clue LUL"
            ;;
        "!link")
            ;&
        "!play")
            if [[ "$CHAN" == "badcop_" ]]; then
                reply "Here is my LD compo entry: https://badcop.games/LD50"
            fi
            ;;
        "!dice")
            ;&
        "!rollycubes")
            if lsof -i:3000 &> /dev/null; then
                reply "Join my game: https://rollycubes.live"
            else
                reply "rollycubes is not running right now :("
            fi
            ;;
        "!lurk")
            reply "sounds good, $name :)"
            ;;
        "!bot")
            reply "i live here: https://github.com/cgsdev0/dotfiles/blob/main/bin/twitch-cmds/twitch-chat"
            ;;
        *"haha"*)
            reply "lol"
            ;;
        *"heh"*)
            reply "haha"
            ;;
        *"emacs"*)
            reply "vim is better"
            ;;
        *"vim"*)
            reply "emacs is better"
            ;;
    esac
}
parsechat() {
    color=$(echo -n "$1" | grep -oP "color=.*?;" | cut -d'=' -f2- | tr -d '#;\n')
    name=$(echo -n "$1" | grep -oP "display-name=.*?;" | cut -d'=' -f2- | tr -d ';\n')
    msg=$(echo -n "$1" | grep -oP "PRIVMSG.*$" | cut -d':' -f2-)
    if [[ "${#color}" != "6" ]]; then
        color=${"$(echo "$name" | md5sum)":3:9}
    fi
    if [[ "$1" == *"custom-reward-id=15569ace-d070-4037-986d-967f44196d74;"* ]]; then
        colorize $color
        echo "$name summoned a cow!"
        cowsay "$msg"
    elif [[ "$msg" == "!lolcat "* ]]; then
        colorize $color
        printf "$name"
        printf '\033[0m'
        echo -n ": "
        echo "$msg" | cut -d' ' -f2- | lolcat
    elif [[ "$msg" == $'\x01'"ACTION "* ]]; then
        colorize $color
        echo "$name" $(echo "$msg" | cut -d' ' -f2-)
    else
        colorize $color
        printf "$name"
        printf '\033[0m'
        echo -n ": "
        echo "$msg"
    fi
}
reqreader() {
  while IFS= read -r line; do
    [[ $line == "PING"* ]] && echo "PONG :tmi.twitch.tv\r";
    [[ "$line" == "SYSTEM_EV "* ]] && reply "$(echo $line | sed 's/^SYSTEM_EV //')"
    [[ "$line" == *" PRIVMSG "* ]] && (parsechat "$line" 1>&2; parsecmd "$line")
  done;
  exit 0;
}
(echo "CAP REQ :twitch.tv/tags twitch.tv/commands\r\nPASS oauth:${TWITCH_ACCESS_TOKEN}\r\nNICK goodcop_\r\nJOIN #${CHAN}\r"; \
    </tmp/twitch_tunnel | reqreader) \
    | websocat_linux64 wss://irc-ws.chat.twitch.tv:443 \
    | >/tmp/twitch_tunnel
